import '.justfiles/base.just'

# Make local node binaries available like npm does
export PATH := "node_modules/.bin:" + env_var("PATH")


################################################################################
#                                     CDK                                      #
################################################################################

# Synthesize CloudFormation template
[group('cdk')]
synth image_tag:
    "{{ justfile_dir() }}/scripts/deploy_test_from_local.sh" "synth" "{{ image_tag }}"

# Deploy ephemeral CDK stack to test environment
[group('cdk')]
deploy-test image_tag:
    "{{ justfile_dir() }}/scripts/deploy_test_from_local.sh" "deploy" "{{ image_tag }}" \
    --no-rollback --require-approval never

# Rollback ephemeral test deployment
[group('cdk')]
rollback-test image_tag:
    "{{ justfile_dir() }}/scripts/deploy_test_from_local.sh" "rollback" "{{ image_tag }}"

[group('cdk')]
destroy-test image_tag:
    "{{ justfile_dir() }}/scripts/deploy_test_from_local.sh" "destroy" "{{ image_tag }}"

################################################################################
#                              Quality & Testing                               #
################################################################################

# compile typescript
[group('check')]
tsc:
    tsc -p tsconfig.json

# prettier
[group('check')]
_fmt *flags:
    prettier {{flags}} .

# prettier (dev)
[group('check')]
fmt: (_fmt "--write")

# prettier (CI)
[group('check')]
fmt-check: (_fmt "--check")

# eslint
[group('check')]
_lint *flags:
    eslint {{flags}} .

# eslint (dev)
[group('check')]
lint: (_lint "--fix")

# eslint (CI)
[group('check')]
lint-check: _lint

# unit tests w/ vitest
[group('test')]
test:
    vitest
