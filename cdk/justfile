import '.justfiles/base.just'

# Make local node binaries available like npm does
export PATH := "node_modules/.bin:" + env_var("PATH")

################################################################################
#                                  CDK Tasks                                   #
################################################################################

image_tag := ""
ephemeral_ns := ""
commit_hash := ""
stage := ""

# Private task to run CDK deployment script
[group('cdk')]
_cdk command image_tag=image_tag ephemeral_ns=ephemeral_ns commit_hash=commit_hash stage=stage:
    "{{ justfile_dir() }}/scripts/run_cdk.sh" "{{ command }}" \
    --ci "{{ ci_mode }}" \
    --image-tag "{{ image_tag }}" \
    --ephemeral-ns "{{ ephemeral_ns }}" \
    --commit-hash "{{ commit_hash }}" \
    --stage "{{ stage }}"

# Synthesize CloudFormation template (local)
[group('cdk')]
synth: (_cdk "synth")

# Deploy ephemeral CDK stack to test environment (local)
[group('cdk')]
deploy: (_cdk "deploy")

# Rollback ephemeral test deployment
[group('cdk')]
rollback: (_cdk "rollback")

# Destroy ephemeral stack (local)
[group('cdk')]
destroy: (_cdk "destroy")


################################################################################
#                              Quality & Testing                               #
################################################################################

# compile typescript
[group('check')]
tsc:
    tsc -p tsconfig.json

# prettier
[group('check')]
_fmt *flags:
    prettier {{flags}} .

# prettier (dev)
[group('check')]
fmt: (_fmt "--write")

# prettier (CI)
[group('check')]
fmt-check: (_fmt "--check")

# eslint
[group('check')]
_lint *flags:
    eslint {{flags}} .

# eslint (dev)
[group('check')]
lint: (_lint "--fix")

# eslint (CI)
[group('check')]
lint-check: _lint

# unit tests w/ vitest
[group('test')]
test:
    vitest
